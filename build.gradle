plugins {
  id "com.gorylenko.gradle-git-properties" version "2.4.2"
  id 'io.freefair.aggregate-javadoc' version '8.7.1'
  id "io.freefair.javadoc-links" version "8.6"
}

ext {
  batikVersion = '1.17'
  junit5Version = '5.10.3'
  log4jVersion = '2.23.1'
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }
}

dependencies {
  rootProject.subprojects { subproject ->
    subproject.plugins.withId("java") {
      javadoc subproject
    }
  }
}

subprojects {
  group = 'com.github.syoon2.pdg'
  version = '2.1.0-dev'

  apply plugin: 'java'
  apply plugin: "com.gorylenko.gradle-git-properties"
  apply plugin: "io.freefair.javadoc-links"

  java {
    // Assume Java 11
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    // JUnit 5 / Jupiter
    testImplementation("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit5Version}")

    // Apache Commons
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.16.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.16.1'

    // Apache Log4j2
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"

  }

  test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true
  }

  tasks.named('test', Test) {
      useJUnitPlatform()
    }

  // Options when compiling tests
  compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
      '-Xlint:unchecked',
      '-Xlint:deprecation'
    ]
  }

  task buildDependenciesFolder(type: Copy) {
    from configurations.implementation
    into './dependencies'
  }
}

project(':pdg-api') {
  jar { enabled = true }
}

project(':pdg-launcher') {
  dependencies {
    implementation project(':pdg-api')
  }
}

allprojects {
  tasks.withType(JavaCompile) {
    dependsOn generateGitProperties
    options.encoding = 'UTF-8'
    options.compilerArgs += [
      '-Xlint:unchecked',
      '-Xlint:deprecation'
    ]
  }
  gitProperties {
    gitPropertiesName = "git.properties"
  }
  task printAllDependencies(type: DependencyReportTask) {}
  tasks.withType(Jar) {
    manifest {
      attributes('Implementation-Title': project.name,
                 'Implementation-Version': project.version)
    }
  }

}
