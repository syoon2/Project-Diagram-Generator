plugins {
  id 'java'
  id "com.gorylenko.gradle-git-properties" version "2.4.1"
  id "io.freefair.javadoc-links" version "8.3"
}


repositories {
  mavenLocal()
  mavenCentral()
}

subprojects {
  group = 'com.github.syoon2.pdg'
  version = '2.0.0-SNAPSHOT'

  apply plugin: 'java'
  apply plugin: "com.gorylenko.gradle-git-properties"
  apply plugin: "io.freefair.javadoc-links"

  java {
    // Assume Java 11
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    // JUnit 5 / Jupiter
    testImplementation('org.junit.jupiter:junit-jupiter:5.10.0')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.10.0')

    // Apache Commons
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.13.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.13.0'

    // Apache Log4j2
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.20.0'

    // graphviz-java
    implementation group: 'guru.nidi', name: 'graphviz-java', version: '0.18.1'

  }

  test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true
  }

  tasks.named('test', Test) {
      useJUnitPlatform()
    }

  // Options when compiling tests
  compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
      '-Xlint:unchecked',
      '-Xlint:deprecation'
    ]
  }

  task buildDependenciesFolder(type: Copy) {
    from configurations.implementation
    into './dependencies'
  }
}

project(':project-diagram-generator-api') {
  jar { enabled = true }
}

project(':project-diagram-generator-gui') {
  dependencies {
    implementation project(':project-diagram-generator-api')
  }
}

allprojects {
  tasks.withType(JavaCompile) {
    dependsOn generateGitProperties
    options.encoding = 'UTF-8'
    options.compilerArgs += [
      '-Xlint:unchecked',
      '-Xlint:deprecation'
    ]
  }
  gitProperties {
    gitPropertiesName = "git.properties"
  }
  task printAllDependencies(type: DependencyReportTask) {}
  tasks.withType(Jar) {
    manifest {
      attributes('Implementation-Title': project.name,
                 'Implementation-Version': project.version)
    }
  }

}
